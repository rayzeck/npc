prt_fild02,305,16,5	script	???#1	4_crack,{
	warp "que_moon",33,12;
	end;
}

que_moon,34,12,5	script	???#2	4_crack,{
	warp "prt_fild02",305,16;
	end;
}
que_moon,33,41,6	script	Shinobi Desconhecido	4_f_lucile,{
function Add; function Chk; function Slot; function A_An;
	if(.Shops$ != "") set .@i,1;
	else {
		mes "[Shinobi Desconhecido]";
		mes "Selecione qual dos Equipamentos";
		set .@menu$,"";
		for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1)
			set .@menu$, .@menu$+.Shops$[.@i]+":";
		set .@i, select(.@menu$); }
		close2;
	dispbottom "Selecione um item por vez.";
	callshop "NEWqshop"+.@i,1;
	npcshopattach "NEWqshop"+.@i;
	end;
function Add {
	if (getitemname(getarg(1))=="null") {
		debugmes "Quest reward #"+getarg(1)+" invalid (skipped)."; return; }
	for(set .@n,5; .@n<127; set .@n,.@n+2) {
		if (!getarg(.@n,0)) break;
		if (getitemname(getarg(.@n))=="null") {
			debugmes "Quest requirement #"+getarg(.@n)+" invalid (skipped)."; return; } }
	for(set .@i,2; .@i<.@n; set .@i,.@i+1)
		set getd(".q_"+getarg(1)+"["+(.@i-2)+"]"), getarg(.@i);
	npcshopadditem "NEWqshop"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	sleep 1;
	return; }
function Chk {
	if (getarg(0)<getarg(1)) { set @qe0,1; return "^FF0000"; }
	else return "^00FF00"; }
function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot){
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),11)>0) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$; } }
function A_An {
	setarray .@A$[0],"a","e","i","o","u";
	set .@B$, "_"+getarg(0);
	for(set .@i,0; .@i<5; set .@i,.@i+1)
		if (compare(.@B$,"_"+.@A$[.@i])) return "an "+getarg(0);
	return "a "+getarg(0); }

OnBuyItem:
	set .@q[0],@bought_nameid;
	copyarray .@q[1],getd(".q_"+@bought_nameid+"[0]"),getarraysize(getd(".q_"+@bought_nameid+"[0]"));
	if (!.@q[1]) { message strcharinfo(0),"Ocorreu um Erro."; end; }
	mes "[Shinobi Desconhecido]";
	mes "Premio: ^0055FF"+((.@q[1]>1)?.@q[1]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "Requerimentos:";
	if (.@q[2]) mes " > "+Chk(Zeny,.@q[2])+.@q[2]+" Zeny^000000";
	if (.@q[3]) mes " > "+Chk(getd(.Points$[0]),.@q[3])+.@q[3]+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+.@q[3]+")^000000";
	if (.@q[4]) for(set .@i,4; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1])+((.DisplayID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+.@q[.@i+1]+")^000000";
	next;
	set @qe1, getiteminfo(.@q[0],5); set @qe2, getiteminfo(.@q[0],11);
	addtimer 1000, strnpcinfo(1)+"::OnEnd";
	while(1){
		switch(select(" - Fazer ^0055FF"+getitemname(.@q[0])+"^000000: - ^777777Cancelar^000000")) {
			case 1:
				if (@qe0) { 
					mes "[Shinobi Desconhecido]";
					mes "Você está faltando um ou mais requisitos de missão.";
					close; }
				if (!checkweight(.@q[0],.@q[1])) {
					mes "[Shinobi Desconhecido]";
					mes "^FF0000Você Precisa "+(((.@q[1]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" Capacidade de peso adicional para completar este comércio.^000000";
					close; }
				if (.@q[2]) set Zeny, Zeny-.@q[2];
				if (.@q[3]) setd .Points$[0], getd(.Points$[0])-.@q[3];
				if (.@q[4]) for(set .@i,4; .@i<getarraysize(.@q); set .@i,.@i+2)
					delitem .@q[.@i],.@q[.@i+1];
				getitem .@q[0],.@q[1];
				if (.Announce) announce strcharinfo(0)+" acaba de fazer "+A_An(getitemname(.@q[0]))+"!",0;
				specialeffect2 699;
				close;
			case 2:
				close; } }
OnEnd:
	if (@qe6) { atcommand "@changelook 3 "+@qe3; atcommand "@changelook 1 "+@qe4; atcommand "@changelook 2 "+@qe5; }
	for(set .@i,0; .@i<7; set .@i,.@i+1) setd "@qe"+.@i,0;
	end;
OnInit:
// --------------------- Config ---------------------
// Custom points, if needed: "<variable>","<name to display>"
	setarray .Points$[0],"#CASHPOINTS","Cash Points";

	set .Announce,1;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,1;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .DisplayID,1;	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,0;	// Show Zeny cost, if any? (1: yes / 0: no)

// Shop categories, if needed: "<Shop 1>","<Shop 2>"{,...};
// Duplicate dummy data for any additional shops (bottom of script).
// If no categories, use the second line instead (remove //).

	setarray .Shops$[1],"Colete","Pergaminho","Arma","Capa","Sandalia","Amuleto";
	// set .Shops$,"n/a";

// Add(<shop number>,<reward ID>,<reward amount>,<Zeny cost>,<point cost>,
//     <required item ID>,<required item amount>{,...});
// Shop number corresponds with order above (default is 1).
// Note: Do NOT use a reward item more than once!

	Add(1,2312,1,10000000,0,769,20,770,1,771,1);
	Add(2,2126,1,10000000,0,757,20,758,1,759,1);	
	Add(3,1101,1,10000000,0,760,20,761,1,762,1);	
	Add(3,1102,1,10000000,0,760,20,761,1,762,1);	
	Add(3,1103,1,10000000,0,760,20,761,1,762,1);	
	Add(3,1104,1,10000000,0,760,20,761,1,762,1);	
	Add(3,1105,1,10000000,0,760,20,761,1,762,1);	
	Add(3,1106,1,10000000,0,760,20,761,1,762,1);	
	Add(3,1107,1,10000000,0,760,20,761,1,762,1);
	Add(3,1108,1,10000000,0,760,20,761,1,762,1);
	Add(3,1109,1,10000000,0,760,20,761,1,762,1);
	Add(3,1110,1,10000000,0,760,20,761,1,762,1);
	Add(3,1111,1,10000000,0,760,20,761,1,762,1);
	Add(3,1112,1,10000000,0,760,20,761,1,762,1);
	Add(3,1113,1,10000000,0,760,20,761,1,762,1);
	Add(3,1235,1,10000000,0,760,20,761,1,762,1);
	Add(4,2511,1,10000000,0,772,20,773,1,774,1);
	Add(5,2415,1,10000000,0,766,20,767,1,768,1);
	Add(6,2631,1,5000000,0,763,20,764,1,765,1);
// --------------------------------------------------
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1)
		npcshopdelitem "NEWqshop"+.@i,505;
	end;
}

// -------- Dummy data (duplicate as needed) --------
-	shop	NEWqshop1	-1,505:-1
-	shop	NEWqshop2	-1,505:-1
-	shop	NEWqshop3	-1,505:-1
-	shop	NEWqshop4	-1,505:-1
-	shop	NEWqshop5	-1,505:-1
-	shop	NEWqshop6	-1,505:-1