/*.-----------------------------------------------------------------------------.
  .          ____                          __                                   .
  .         /\  _`\                       /\ \__  __                            .
  .         \ \ \/\_\  _ __    __     __  \ \ ,_\/\_\  __  __     __            .
  .          \ \ \/_/_/\`'__\/'__`\ /'__`\ \ \ \/\/\ \/\ \/\ \  /'__`\          .
  .           \ \ \s\ \ \ \//\  __//\ \d\.\_\ \ \_\ \ \ \ \_/ |/\  __/          .
  .            \ \____/\ \_\\ \____\ \__/.\_\\ \__\\ \_\ \___/ \ \____\         .
  .             \/___/  \/_/ \/____/\/__/\/_/ \/__/ \/_/\/__/   \/____/         .
  .                                                                             .
  .                  2014~2016 © Creative Services and Developent               .
  .                            www.creativesd.com.br                            .
  .                          www.sbkmod.ragnagreen.com                          .
  .-----------------------------------------------------------------------------.
  . Script:                                                                     .
  .    1ª Arena de Krieger von Midgard                                          .
  .-----------------------------------------------------------------------------.
  . Autor: Romulo SM (sbk_) (HerculesWS)                           Versão: 6.0  .
  .                                                                             .
  . Compátivel:	eAthena e derivações.                    	                    .
  .-----------------------------------------------------------------------------.
  . Descrição:                                                                  .
  .    Arena no qual os Exércitos se confrontam diretamente.                    .
  *------------------------------------------------------------------------------*/

// Engine das Batalhas Campais de Krieger von Midgard
// -------------------------------------------------------
bat_room,149,175,5	script	Time Jiraiya#a::KvM01R_Guillaume	69,{
	end;
OnInit:
	waitingroom "5 jogadores para iniciar!",6,"KvM01_BG_Out::OnGuillaumeJoin",1;
	end;
OnEnterBG:
	set $@KvM01BG_id1, waitingroom2bg("bat_c01",52,129,"KvM01_BG::OnGuillaumeQuit","KvM01_BG::OnGuillaumeDie");
	end;
}

bat_room,157,175,3	script	Time Orochimaru#b::KvM01R_Croix	70,{
	end;

OnInit:
	waitingroom "5 jogadores para iniciar!",6,"KvM01_BG_Out::OnCroixJoin",1;
	end;

OnEnterBG:
	set $@KvM01BG_id2, waitingroom2bg("bat_c01",147,55,"KvM01_BG::OnCroixQuit","KvM01_BG::OnCroixDie");
	end;
}

// Starting Line
//============================================================
-	script	KVM01::CellEffect	-1,{
	end;
OnKvM01One:
	specialeffect EF_CHANGECOLD;
	end;
OnKvM01Two:
	specialeffect EF_CHANGEPOISON;
	end;
}

bat_c01,54,124,3	duplicate(CellEffect)	#RedcellA1-1	139
bat_c01,55,124,3	duplicate(CellEffect)	#RedcellA2-1	139
bat_c01,56,124,3	duplicate(CellEffect)	#RedcellA3-1	139
bat_c01,57,124,3	duplicate(CellEffect)	#RedcellA4-1	139
bat_c01,57,125,3	duplicate(CellEffect)	#RedcellA5-1	139
bat_c01,57,126,3	duplicate(CellEffect)	#RedcellA6-1	139
bat_c01,57,127,3	duplicate(CellEffect)	#RedcellA7-1	139

bat_c01,145,59,3	duplicate(CellEffect)	#RedcellB1-1	139
bat_c01,144,59,3	duplicate(CellEffect)	#RedcellB2-1	139
bat_c01,143,59,3	duplicate(CellEffect)	#RedcellB3-1	139
bat_c01,142,59,3	duplicate(CellEffect)	#RedcellB4-1	139
bat_c01,142,56,3	duplicate(CellEffect)	#RedcellB5-1	139
bat_c01,142,57,3	duplicate(CellEffect)	#RedcellB6-1	139
bat_c01,142,58,3	duplicate(CellEffect)	#RedcellB7-1	139

bat_c01,54,128,3	script	#A_camp_start01	139,4,4,{
	end;
OnInit:
	disablenpc "#A_camp_start01";
	end;
OnEnable:
	enablenpc "#A_camp_start01";
	end;
OnDisable:
	disablenpc "#A_camp_start01";
	end;
OnTouch:
	set Bat_Team,1;
	setquest 6025;
	end;
}

bat_c01,146,56,3	script	#B_camp_start01	139,4,4,{
	end;
OnInit:
	disablenpc "#B_camp_start01";
	end;
OnEnable:
	enablenpc "#B_camp_start01";
	end;
OnDisable:
	disablenpc "#B_camp_start01";
	end;
OnTouch:
	set Bat_Team,2;
	setquest 6025;
	end;
}

// Battleground Engine
//============================================================
-	script	KvM01_BG	-1,{
	end;

OnInit:
	setwall "bat_c01",54,122,6,7,0,"batc01wall_a";
	setwall "bat_c01",55,122,5,7,0,"batc01wall_b";
	setwall "bat_c01",140,56,6,7,0,"batc01wall_c";
	setwall "bat_c01",140,57,5,7,0,"batc01wall_d";
	disablenpc "Haruno Sakura#KVM01A";
	disablenpc "Haruno Sakura#KVM01B";
	end;

OnGuillaumeQuit:
	//set BG_Delay_Tick, gettimetick(2) + 1200;
OnGuillaumeDie:
	if( $@KvM01BG == 2 )
	{
		set .Guillaume_Count, .Guillaume_Count - 1;
		bg_updatescore "bat_c01",.Guillaume_Count,.Croix_Count;
		if( .Guillaume_Count < 1 ) donpcevent "KvM01_BG::OnCroixWin";
		else {
			mapannounce "bat_c01", "O número de sobreviventes do time Jiraiya é "+.Guillaume_Count+".",bc_map,"0x00ff00";
			sleep 500;
			mapannounce "bat_c01", "O número de sobreviventes do time Orochimaru é "+.Croix_Count+".",bc_map,"0x00ff00";
		}
	}
	end;

OnCroixQuit:
	//set BG_Delay_Tick, gettimetick(2) + 1200;
OnCroixDie:
	if( $@KvM01BG == 2 )
	{
		set .Croix_Count, .Croix_Count - 1;
		bg_updatescore "bat_c01",.Guillaume_Count,.Croix_Count;
		if( .Croix_Count < 1 ) donpcevent "KvM01_BG::OnGuillaumeWin";
		else {
			mapannounce "bat_c01", "O número de sobreviventes do time Jiraiya é  "+.Guillaume_Count+".",bc_map,"0x00ff00";
			sleep 500;
			mapannounce "bat_c01", "O número de sobreviventes do time Orochimaru é "+.Croix_Count+".",bc_map,"0x00ff00";
		}
	}
	end;

OnStart:
	disablenpc "Haruno Sakura#KVM01A";
	disablenpc "Haruno Sakura#KVM01B";
	set $@KvM01BG_Victory, 0;
	// Warp Teams
	bg_warp $@KvM01BG_id1,"bat_c01",53,128;
	bg_warp $@KvM01BG_id2,"bat_c01",146,55;
	initnpctimer;
	end;

OnTimer1000:
	mapannounce "bat_c01", "Em 1 minuto, a Ranqueada vai começar.",bc_map,"0x00ff00";
	end;

OnTimer3000:
	mapannounce "bat_c01", "O tempo limite de batalha é 5 minutos.",bc_map,"0x00ff00";
	end;

OnTimer6000:
	mapannounce "bat_c01", "Se preparem!",bc_map,"0x00ff00";
	donpcevent "#A_camp_start01::OnEnable";
	donpcevent "#B_camp_start01::OnEnable";
	end;

OnTimer13000:
	donpcevent "#A_camp_start01::OnDisable";
	donpcevent "#B_camp_start01::OnDisable";
	end;

OnTimer30000:
	mapannounce "bat_c01", "30 segundos para o início da partida.",bc_map,"0x00ff00";
	end;

OnTimer45000:
	mapannounce "bat_c01", "15 segundos para o início da partida.",bc_map,"0x00ff00";
	donpcevent "::OnKvM01One";
	end;

OnTimer50000:
	mapannounce "bat_c01", "10 segundos para o início da partida.",bc_map,"0x00ff00";
	donpcevent "::OnKvM01Two";
	end;

OnTimer55000:
	mapannounce "bat_c01", "5 segundos para o início da partida.",bc_map,"0x00ff00";
	donpcevent "::OnKvM01One";
	end;

OnTimer59000:
	mapannounce "bat_c01", "A partida iniciou!",bc_map,"0x00ff00";
	donpcevent "::OnKvM01Two";
	end;

OnTimer61000:
	// Team Members
	set .Guillaume_Count, bg_get_data($@KvM01BG_id1, 0);
	set .Croix_Count, bg_get_data($@KvM01BG_id2, 0);
	if( .Guillaume_Count < 5 || .Croix_Count < 5 )
	{
		set $@KvM01BG_Victory, 3;
		set $@KvM01BG, 3;
		mapannounce "bat_c01","Não temos shinobis suficientes para o início.",1,0x696969;
		stopnpctimer;
		donpcevent "KvM01_BG::OnStop";
		end;
	}
	set $@KvM01BG, 2; // Playing
	bg_warp $@KvM01BG_id1,"bat_c01",61,120;
	bg_warp $@KvM01BG_id2,"bat_c01",138,63;
	end;

OnTimer300000:
	mapannounce "bat_c01", "1 minuto para o fim da partida.",bc_map,"0x00ff00";
	end;

OnTimer330000:
	mapannounce "bat_c01", "30 segundos para o fim da partida.",bc_map,"0x00ff00";
	end;

OnTimer345000:
	mapannounce "bat_c01", "15 segundos para o fim da partida.",bc_map,"0x00ff00";
	end;

OnTimer350000:
	mapannounce "bat_c01", "10 segundos para o fim da partida.",bc_map,"0x00ff00";
	end;

OnTimer355000:
	mapannounce "bat_c01", "5 segundos para o fim da partida.",bc_map,"0x00ff00";
	end;

OnTimer360000:
	mapannounce "bat_c01", "A partida acabou!",bc_map,"0x00ff00";
	if( .Croix_Count > .Guillaume_Count )
		donpcevent "KvM01_BG::OnCroixWin";
	else if( .Croix_Count < .Guillaume_Count )
		donpcevent "KvM01_BG::OnGuillaumeWin";
	else
	{ // Draw Game
		set $@KvM01BG, 3;
		set $@KvM01BG_Victory, 3;
		mapannounce "bat_c01", "O número de sobreviventes do time Jiraiya é "+.Guillaume_Count+".",bc_map,"0x00ff00";
		sleep 500;
		mapannounce "bat_c01", "O número de sobreviventes do time Orochimaru é "+.Croix_Count+".",bc_map,"0x00ff00";
		sleep 500;
		mapannounce "bat_c01", "Foi um empate!",bc_map,"0x00ff00";
		donpcevent "KvM01_BG::OnStop";
	}
	end;

OnGuillaumeWin:
	set $@KvM01BG, 3;
	set $@KvM01BG_Victory, 1;
	mapannounce "bat_c01", "Time Jiraiya venceu!",bc_map,"0x00ff00";
	mapannounce "bat_c01", "Parabéns a todos!",bc_map,"0x00ff00";
	donpcevent "KvM01_BG::OnStop";
	end;

OnCroixWin:
	set $@KvM01BG, 3;
	set $@KvM01BG_Victory, 2;
	mapannounce "bat_c01", "Time Orochimaru venceu!",bc_map,"0x00ff00";
	mapannounce "bat_c01", "Parabéns a todos!",bc_map,"0x00ff00";
	donpcevent "KvM01_BG::OnStop";
	end;

OnStop:
	stopnpctimer;
	enablenpc "Haruno Sakura#KVM01A";
	enablenpc "Haruno Sakura#KVM01B";
	// Warp Teams
	bg_warp $@KvM01BG_id1,"bat_c01",53,128;
	bg_warp $@KvM01BG_id2,"bat_c01",146,55;
	donpcevent "KvM01_BG_Out::OnBegin";
	end;
}

-	script	KvM01_BG_Out	-1,{
	end;

OnBegin:
	initnpctimer;
	end;

OnTimer1000:
	mapannounce "bat_c01", "Fale com Sakura para ganhar a premiação!",bc_map,"0x00ff00";
	end;

OnTimer3000:
	mapannounce "bat_c01", "Em 30 segundos, a oficial será desativada!",bc_map,"0x00ff00";
	end;

OnTimer5000:
	mapannounce "bat_c01", "Você só ganhará o prêmio se falar com a oficial, cuidado!",bc_map,"0x00ff00";
	end;

OnTimer55000:
	mapannounce "bat_c01", "Partida encerrada, todos voltaram para a sala.",bc_map,"0x00ff00";
	end;

OnTimer60000:
	stopnpctimer;
	set getvariableofnpc(.Croix_Count,"KvM01_BG"), 0;
	set getvariableofnpc(.Guillaume_Count,"KvM01_BG"), 0;
	set $@KvM01BG_Victory, 0;
	if( $@KvM01BG_id1 ) { bg_destroy $@KvM01BG_id1; set $@KvM01BG_id1, 0; }
	if( $@KvM01BG_id2 ) { bg_destroy $@KvM01BG_id2; set $@KvM01BG_id2, 0; }
	disablenpc "Haruno Sakura#KVM01A";
	disablenpc "Haruno Sakura#KVM01B";
	mapwarp "bat_c01","bat_room",154,150;
	maprespawnguildid "bat_c01",0,3; // Just in case someone else
	bg_updatescore "bat_c01",5,5;
	set $@KvM01BG, 0;

OnGuillaumeJoin:
OnCroixJoin:
	if( $@KvM01BG )
		end;
	set .@Guillaume, getwaitingroomstate(0,"KvM01R_Guillaume");
	set .@Croix, getwaitingroomstate(0,"KvM01R_Croix");

	if( .@Guillaume < 5 || .@Croix < 5 )
		end;

	set $@KvM01BG, 1; // Starting
	donpcevent "KvM01R_Croix::OnEnterBG";
	donpcevent "KvM01R_Guillaume::OnEnterBG";
	donpcevent "KvM01_BG::OnStart";
	end;
}

// Battleground rewards
//============================================================
bat_c01,51,130,5	script	Haruno Sakura#KVM01A	739,{
	if( $@KvM01BG_Victory ) {
		mes "[Haruno Sakura]";
		if( $@KvM01BG_Victory == Bat_Team ) {
			mes "Bom jogo, você é o vencedor!";
			mes "Você ganhou 30 pontos!";
			getitem 735,30;
			close2;
		}
		else {
			mes "Boa sorte na próxima!";
			mes "Apesar da derrota, você ganhou 15 pontos!";
			getitem 735,15;
			close2;
		}
		bg_leave;
		set Bat_Team,0;
		warp "bat_room",154,150;
	}
	end;
}

bat_c01,148,53,1	duplicate(Haruno Sakura#KVM01A)	Haruno Sakura#KVM01B	739